(ns io.zane.plot.matrix)

(defn fill
  "Returns a two-dimensional array of the provided dimensions populated by values
  generated by the zero-argument function f."
  [f width height]
  (let [row-f #(into [] (take width (repeatedly f)))]
    (into [] (take height (repeatedly row-f)))))

(defn in-bounds?
  "Returns whether a point is in a multidimensional matrix."
  [m ks]
  (loop [m m
         ks ks]
    (if-not (seq ks)
      true
      (let [index (first ks)]
        (and (<= 0 index (dec (count m)))
             (recur (nth m index)
                    (rest ks)))))))

(defn extremes
  "Returns a 2-tuple of the minimum and maximum values in the provided sequence."
  [xs]
  [(reduce min xs)
   (reduce max xs)])

(defn string
  "Takes a matrix of characters and converts it into a string by adding newlines
  at the end of each row and then concatenating everything together."
  [m]
  (transduce (comp (map #(reduce str %))
                   (interpose \newline))
             str
             m))
